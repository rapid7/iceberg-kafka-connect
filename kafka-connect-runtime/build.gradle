plugins {
  id "distribution"
}

task downloadIcebergJars {
  doLast {
    def downloadDir = file("$buildDir/libs")
    downloadDir.mkdirs()

    def urls = [
            "https://artifacts.bos.rapid7.com/artifactory/releases/transport/iceberg-fips/1.6.x/1746026749/iceberg-aws-bundle-1.7.0-SNAPSHOT.jar",
            "https://artifacts.bos.rapid7.com/artifactory/releases/transport/iceberg-fips/1.6.x/1746026749/iceberg-azure-bundle-1.7.0-SNAPSHOT.jar"
    ]

    urls.each { urlString ->
      def url = new URL(urlString)
      def fileName = urlString.tokenize("/")[-1]
      def destination = new File(downloadDir, fileName)

      if (!destination.exists()) {
        println "Downloading: ${fileName}"
        destination.withOutputStream { out ->
          url.openStream().withStream { inStream ->
            out << inStream
          }
        }
      } else {
        println "${fileName} already exists."
      }
    }
  }
}

configurations {
  customJars
  hive {
    extendsFrom runtimeClasspath
  }
  all {
    resolutionStrategy.force "org.codehaus.jettison:jettison:1.5.4"
    resolutionStrategy.force "org.xerial.snappy:snappy-java:1.1.10.5"
    resolutionStrategy.force "org.apache.commons:commons-compress:1.26.0"
    resolutionStrategy.force "org.apache.hadoop.thirdparty:hadoop-shaded-guava:1.1.1-tabular"
    resolutionStrategy.force "io.airlift:aircompressor:0.27"
    resolutionStrategy.force "org.apache.parquet:parquet-avro:1.15.1"
    resolutionStrategy.eachDependency { details ->
      if (details.requested.group == "io.netty") {
        details.useVersion "4.1.118.Final"
      }
    }
  }
}

dependencies {
  implementation project(":iceberg-kafka-connect")
  implementation project(":iceberg-kafka-connect-transforms")
  implementation(libs.hadoop.common) {
    exclude group: "log4j"
    exclude group: "org.slf4j"
    exclude group: "ch.qos.reload4j"
    exclude group: "org.apache.avro", module: "avro"
    exclude group: "com.google.guava"
    exclude group: "com.google.protobuf"
    exclude group: "org.apache.curator"
    exclude group: "org.apache.zookeeper"
    exclude group: "org.apache.kerby"
    exclude group: "org.apache.hadoop", module: "hadoop-auth"
    exclude group: "org.apache.commons", module: "commons-configuration2"
    exclude group: "org.apache.hadoop.thirdparty", module: "hadoop-shaded-protobuf_3_7"
    exclude group: "org.eclipse.jetty"
  }
  implementation libs.commonsBeanutils
  implementation libs.protobuf.java
  implementation libs.derby
  implementation libs.jetty.http
  implementation libs.jetty.server
  implementation libs.logback.classic
  implementation libs.logback.core
  implementation libs.jackson.core
  implementation libs.guava
  implementation libs.parquet.avro
  implementation (group: 'org.apache.iceberg', name: 'iceberg-data', version: '1.6.1') {
    exclude group: 'org.apache.avro', module: 'avro'
    exclude group: 'org.apache.parquet', module: 'avro'
  }

  implementation (group: 'org.apache.iceberg', name: 'iceberg-parquet', version: '1.6.1') {
    exclude group: 'org.apache.avro', module: 'avro'
    exclude group: 'org.apache.parquet', module: 'avro'
  }


  runtimeOnly libs.bundles.iceberg.ext

  hive libs.iceberg.hive.metastore
  hive(libs.hive.metastore) {
    exclude group: "org.apache.avro", module: "avro"
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "org.pentaho" // missing dependency
    exclude group: "org.apache.hbase"
    exclude group: "org.apache.logging.log4j"
    exclude group: "co.cask.tephra"
    exclude group: "com.google.code.findbugs", module: "jsr305"
    exclude group: "org.eclipse.jetty.aggregate", module: "jetty-all"
    exclude group: "org.eclipse.jetty.orbit", module: "javax.servlet"
    exclude group: "org.apache.parquet", module: "parquet-hadoop-bundle"
    exclude group: "com.tdunning", module: "json"
    exclude group: "javax.transaction", module: "transaction-api"
    exclude group: "com.zaxxer", module: "HikariCP"
    exclude group: "org.apache.hadoop", module: "hadoop-yarn-server-common"
    exclude group: "org.apache.hadoop", module: "hadoop-yarn-server-applicationhistoryservice"
    exclude group: "org.apache.hadoop", module: "hadoop-yarn-server-resourcemanager"
    exclude group: "org.apache.hadoop", module: "hadoop-yarn-server-web-proxy"
    exclude group: "org.apache.hive", module: "hive-service-rpc"
    exclude group: "com.github.joshelser", module: "dropwizard-metrics-hadoop-metrics2-reporter"
  }
  hive(libs.hadoop.client) {
    exclude group: "org.apache.avro", module: "avro"
    exclude group: "org.slf4j", module: "slf4j-log4j12"
  }

  testImplementation libs.bundles.iceberg

  testImplementation files("$buildDir/libs/iceberg-aws-bundle-1.7.0-SNAPSHOT.jar")
  testImplementation files("$buildDir/libs/iceberg-azure-bundle-1.7.0-SNAPSHOT.jar")

  testImplementation libs.bundles.jackson
  testImplementation libs.bundles.kafka.connect

  testImplementation libs.junit.api
  testImplementation libs.junit.params
  testRuntimeOnly libs.junit.engine

  testImplementation libs.mockito
  testImplementation libs.assertj
  testImplementation libs.awaitility
  testImplementation libs.testcontainers
  testImplementation libs.testcontainers.kafka
  testImplementation libs.http.client
}

processResources {
  filter {
    it.replace("__VERSION__", project.version.toString())
  }
}

// Add a task to verify the custom JARs exist
task verifyCustomJars {
  dependsOn downloadIcebergJars
  doLast {
    def awsBundle = file("$buildDir/libs/iceberg-aws-bundle-1.7.0-SNAPSHOT.jar")
    def azureBundle = file("$buildDir/libs/iceberg-azure-bundle-1.7.0-SNAPSHOT.jar")

    if (!awsBundle.exists()) {
      throw new GradleException("Missing iceberg-aws-bundle-1.7.0-SNAPSHOT.jar in ${awsBundle.absolutePath}")
    }

    if (!azureBundle.exists()) {
      throw new GradleException("Missing iceberg-azure-bundle-1.7.0-SNAPSHOT.jar in ${azureBundle.absolutePath}")
    }

    println "Custom JARs exist:"
    println "- ${awsBundle.absolutePath}"
    println "- ${azureBundle.absolutePath}"
  }
}

// Make sure downloadIcebergJars runs after clean and before other tasks
downloadIcebergJars.mustRunAfter clean

distributions {
  main {
    contents {
      from(processResources.destinationDir) {
        include "manifest.json"
      }
      into("lib/") {
        from configurations.runtimeClasspath
        // Add custom JARs explicitly to the distribution
        from fileTree(dir: "$buildDir/libs", include: [
                "iceberg-aws-bundle-1.7.0-SNAPSHOT.jar",
                "iceberg-azure-bundle-1.7.0-SNAPSHOT.jar"
        ])
      }
      into("doc/") {
        from "$rootDir/LICENSE"
        from "$rootDir/README.md"
      }
      into("assets/") {
        from "$rootDir/logos"
      }
    }
  }
  hive {
    contents {
      from(processResources.destinationDir) {
        include "manifest.json"
      }
      into("lib/") {
        from configurations.hive
      }
      into("doc/") {
        from "$rootDir/LICENSE"
        from "$rootDir/README.md"
      }
      into("assets/") {
        from "$rootDir/logos"
      }
    }
  }
}

publishing {
  publications {
    main(MavenPublication) {
      artifact distZip
    }
    hive(MavenPublication) {
      artifact hiveDistZip
    }
  }
}

tasks.jar.enabled = false

tasks.distTar.enabled = false
tasks.hiveDistTar.enabled = false

tasks.distZip.dependsOn processResources, verifyCustomJars
tasks.hiveDistZip.dependsOn processResources, verifyCustomJars
tasks.installDist.dependsOn processResources, verifyCustomJars
tasks.installHiveDist.dependsOn processResources, verifyCustomJars

// Ensure the verifyCustomJars task depends on downloadIcebergJars
verifyCustomJars.dependsOn downloadIcebergJars

// make sure compileJava also depends on downloadIcebergJars
compileJava.dependsOn downloadIcebergJars

// build the install before test so it can be installed into kafka connect
test.dependsOn installDist

assemble.dependsOn distZip, hiveDistZip